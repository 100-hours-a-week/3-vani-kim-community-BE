plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.vani.week4'
version = '0.0.1-SNAPSHOT'
description = 'backend'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

jar {
    enabled = false
}

bootJar {
    duplicatesStrategy = 'exclude'
}

test {
    useJUnitPlatform()
    jvmArgs '-XX:+EnableDynamicAgentLoading'

    testLogging {
        // 개발 중에는 이 정도면 충분
        events "passed", "failed"
        exceptionFormat "full"
        showStackTraces true

        // 최종 요약
        afterSuite { desc, result ->
            if (!desc.parent) {
                println "\n===== 테스트 결과 ====="
                println "총 ${result.testCount}개 중 성공 ${result.successfulTestCount}개, 실패 ${result.failedTestCount}개"
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'software.amazon.awssdk:s3:2.20.26'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0"
    implementation 'com.github.f4b6a3:ulid-creator:5.2.3'
    implementation "io.jsonwebtoken:jjwt-api:0.11.5"
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
    implementation 'org.springframework.security:spring-security-crypto:6.3.3'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'com.h2database:h2'
}


tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked"
}

tasks.named('test') {
    useJUnitPlatform {
        // 커맨드라인에서 -PincludeTags=unit 처럼 넘겨주면 그것만 실행
        if (project.hasProperty('includeTags')) {
            includeTags project.findProperty('includeTags') as String
        }
        // 제외할 태그가 있으면 제외
        if (project.hasProperty('excludeTags')) {
            excludeTags project.findProperty('excludeTags') as String
        }
    }
}